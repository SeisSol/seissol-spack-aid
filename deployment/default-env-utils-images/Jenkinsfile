properties([
    parameters([
        string(
            defaultValue: 'runner', 
            description: 'agent name which tells where to run a job', 
            name: 'AGENT',
            trim: true),
        booleanParam(
            defaultValue: false, 
            description: 'force docker to build images without using caching', 
            name: 'FORCED_BUILD')
    ])
])

pipeline {
    agent {label 'master'}
    stages {
        stage('CleanWorkspace') {
            steps {
                deleteDir()
            }
        }


        stage('Clone') {
            environment {
                TRIMMED_BRANCH = sh(script:'echo ${GIT_BRANCH##origin/}', returnStdout: true).trim()
            }
            steps {
                git branch: "${env.TRIMMED_BRANCH}", url: "${GIT_URL}"
                script {
                    remoteCacheDir = "~/.seissol-aid"
                }
            }
        }


        stage('BuildAll') {
            steps {
                script {
                    String[][] configList = [["ppc64le", "cuda@10.1.243"], 
                                             ["amd64", "cuda@10.1.243"],
                                             ["arm64", "cuda@11.0.2"]]

                    for(config in configList) {
                        platform = config[0]
                        gpu_lang = config[1]
                                
                        sh "echo ${platform} - ${gpu_lang}"
                        build job: 'env-utils-image', parameters: [
                        string(name: 'OS', value: 'ubuntu-1804'),
                        string(name: 'SPACK_VERSION', value: 'latest'),
                        string(name: 'ARCH_FAMILY', value: "${platform}"),
                        string(name: 'ARCH', value: ''),
                        string(name: 'TARGET_COMPILER', value: 'gcc@8.3.0'),
                        string(name: 'TARGET_MPI', value: 'openmpi@3.1.5+cuda'),
                        string(name: 'TARGET_GPU', value: "${gpu_lang}"),
                        string(name: 'CACHE_DIR', value: "${remoteCacheDir}"),
                        booleanParam(name: 'SINGULARITY_PUSH', value: true),
                        booleanParam(name: 'FORCED_BUILD', value: "${env.FORCED_BUILD}"),
                        string(name: 'AGENT', value: "${env.AGENT}")
                        ]
                    }
                }
            }
        }


        stage('StashingOnRunner') {
            agent {label "${env.AGENT}"}
            steps {
                sh 'mkdir -p ./generated-files'
                sh "cp -r ${remoteCacheDir}/* ./generated-files"
                dir('generated-files') {
                    stash name: "generatedFiles", includes: '**/*', excludes: '**/*.sif'
                }
            }
        }


        stage('UnstashingOnMaster') {
            steps {
                script {
                    pathToGeneratedFiles = "deployment/default-env-utils-images/image-files"
                }

                dir("${pathToGeneratedFiles}") {
                    unstash "generatedFiles"
                }

                script {
                    changedFiles = sh (script: "git status --porcelain ./${pathToGeneratedFiles}", 
                                       returnStdout: true).trim()
                }
            }
        }


        stage('CommitChanges') {
            when {
                expression {
                    !changedFiles.isAllWhitespace()
                }
            }

            steps {
                script {
                    branchName = "${env.GIT_BRANCH}"
                    branchName = branchName.replaceFirst(".+/", "")
                    withCredentials([usernamePassword(credentialsId: 'github-repo',
                                                      usernameVariable: 'GIT_USERNAME',
                                                      passwordVariable: 'GIT_PASSWORD')]){
                        sh """
                        git branch -v
                        git config --local credential.helper "!p() { echo username=\\$GIT_USERNAME; echo password=\\$GIT_PASSWORD; }; p"
                        git add ${pathToGeneratedFiles}
                        git commit -m \"commited by Jenkins: build #${env.BUILD_ID}\"
                        git push origin ${branchName}:${branchName}
                        """
                    }
                }
            }
        }
    }

    post { 
        always {
            sh 'echo "end..."'
        }
    }
}